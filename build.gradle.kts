plugins {
    // this is necessary to avoid the plugins to be loaded multiple times
    // in each subproject's classloader
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.composeMultiplatform) apply false
    alias(libs.plugins.composeCompiler) apply false
    alias(libs.plugins.kotlin.multiplatform) apply false
    alias(libs.plugins.kotlin.serialization) apply false
    alias(libs.plugins.ksp) apply false
    alias(libs.plugins.room) apply false
    alias(libs.plugins.nativeCocoaPods) apply false
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.buildKonfig) apply false
    alias(libs.plugins.kotlinx.atomicfu) apply false
    alias(libs.plugins.androidKotlinMultiplatformLibrary) apply false
    alias(libs.plugins.androidLint) apply false
}

project.gradle.taskGraph.whenReady {
    allTasks.filter { it::class.simpleName?.contains("EmbedAndSign") == true }.forEach {
        logger.warn("Disabling embedding and signing task in project ${it.project.name}")
        it.enabled = false
    }
}

// Synthetic pod installs share one CocoaPods cache, which is not safe for concurrent writers.
abstract class PodInstallLock : BuildService<BuildServiceParameters.None>

val podInstallLock = gradle.sharedServices.registerIfAbsent("podInstallLock", PodInstallLock::class) {
    maxParallelUsages.set(1)
}

subprojects {
    tasks.matching { it.name.startsWith("podInstallSynthetic") }.configureEach {
        usesService(podInstallLock)
    }
}
